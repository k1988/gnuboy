
AC_INIT(cpu.c)

CFLAGS="$CFLAGS"

AC_PROG_CC
AC_PROG_INSTALL

test "$cross_compiling" = "yes" || AC_C_BIGENDIAN

test "$ac_cv_c_bigendian" = "no" && ENDIAN="-DIS_LITTLE_ENDIAN"

dnl AC_CHECK_SIZEOF(short)
dnl AC_CHECK_SIZEOF(int)
dnl AC_CHECK_SIZEOF(long)

dnl AC_FUNC_MMAP

AC_CHECK_FUNCS(usleep, ,[
AC_CHECK_FUNCS(select, ,[
AC_MSG_ERROR(your system must support either usleep or select)
])])

LIBS="$LIBS -L/usr/local/lib -L/usr/X11R6/lib"

HAVE_VGA=yes
AC_CHECK_LIB(vga, vga_init, [
AC_CHECK_HEADERS(vga.h vgakeyboard.h, ,[
AC_MSG_WARN(svgalib found but headers are missing!!)
HAVE_VGA=
])], [HAVE_VGA=])

test "$HAVE_VGA" && TARGETS="$TARGETS sgnuboy"

HAVE_SDL=yes
AC_CHECK_LIB(SDL, SDL_Init, [
AC_CHECK_HEADERS(SDL/SDL.h, ,[
AC_MSG_WARN(SDL found but headers are missing!!)
HAVE_SDL=
])], [HAVE_SDL=])

test "$HAVE_SDL" && TARGETS="$TARGETS sdlgnuboy"

AC_PATH_X

if test "$no_x" != "yes" ; then
TARGETS="$TARGETS xgnuboy"
AC_CHECK_LIB(Xext, XShmCreateImage)
AC_CHECK_HEADERS(sys/ipc.h sys/shm.h X11/extensions/XShm.h)
XINCS="-I$x_includes"
XLIBS="-L$x_libraries"
fi

AC_CHECK_HEADERS(sys/soundcard.h, [SOUND=sys/oss/oss.o], [SOUND=sys/dummy/nosound.o])

AC_ARG_ENABLE(debug,    [  --enable-debug          include debugging symbols], [])
AC_ARG_ENABLE(profile,  [  --enable-profile        enable performance profiling], [])
AC_ARG_ENABLE(optimize, [  --enable-optimize=LEVEL select optimization level (full,low,none)], [], [enable_optimize=yes])
AC_ARG_ENABLE(asm,      [  --enable-asm            use hand-optimized asm cores], [], [enable_asm=yes])


if test "$enable_debug" = yes ; then
AC_MSG_RESULT(including debugging symbols)
CFLAGS="$CFLAGS -g"
fi

if test "$enable_profile" = yes ; then
AC_MSG_RESULT(enabling performance profiling)
CFLAGS="$CFLAGS -pg"
fi

case "$enable_optimize" in
yes|full)
AC_MSG_RESULT(producing heavily optimized code)

case `uname -m` in i?86)

test -f /proc/cpuinfo && case `grep "model name" /proc/cpuinfo` in
*AMD-K6*) case `gcc --version` in 2.9*) CFLAGS="$CFLAGS -mcpu=k6" ;; esac ;;
*Pentium*|*586*) case `gcc --version` in 2.9*) CFLAGS="$CFLAGS -mcpu=pentium" ;; esac ;;
*486*) CFLAGS="$CFLAGS -m486" ;;
esac

CFLAGS="$CFLAGS -O3 -DALLOW_UNALIGNED_IO \
 -fstrength-reduce -fthread-jumps \
 -fcse-follow-jumps -fcse-skip-blocks -frerun-cse-after-loop \
 -fschedule-insns -fschedule-insns2 -fexpensive-optimizations \
 -fforce-mem -fforce-addr" ;;
*) CFLAGS="$CFLAGS -O3" ;;

esac

if test "$enable_debug" != yes -a "$enable_profile" != yes ; then
CFLAGS="$CFLAGS -fomit-frame-pointer"
fi ;;

low)

AC_MSG_RESULT(using minimal optimizations)
CFLAGS="$CFLAGS -O2" ;;

esac

if test "$enable_asm" = yes ; then
case `uname -m` in
i?86)
AC_MSG_RESULT(using optimized i386 cores)
ASM="-DUSE_ASM -I./asm/i386" ; ASM_OBJS="asm/i386/cpu.o asm/i386/lcd.o" ;;
*)
AC_MSG_RESULT(no optimized asm core available for `uname -m`) ;;
esac
fi


AC_SUBST(ENDIAN)
AC_SUBST(SOUND)
AC_SUBST(ASM)
AC_SUBST(ASM_OBJS)
AC_SUBST(TARGETS)
AC_SUBST(XINCS)
AC_SUBST(XLIBS)

AC_CONFIG_HEADER(sys/nix/config.h)
AC_OUTPUT(Makefile)



